buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE")
    }
}

plugins {
    id 'net.ltgt.apt' version '0.8'
    id "org.flywaydb.flyway" version "5.0.3"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'war'

war {
    baseName = 'food-finder'
    version =  '0.9.0'
}

repositories {
    mavenCentral()
    maven { url "https://repository.jboss.org/nexus/content/repositories/releases" }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    //Spring related
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-security")

    //Database configuration
    compile("mysql:mysql-connector-java:6.0.6")
    compile("com.zaxxer:HikariCP:2.7.4")
    compile("org.flywaydb:flyway-core:5.0.3")

    //Boilerplate generation
    compile("org.projectlombok:lombok:1.16.18")
    compile("org.mapstruct:mapstruct-jdk8:1.2.0.Final")
    apt("org.projectlombok:lombok:1.16.18")
    apt("org.mapstruct:mapstruct-processor:1.2.0.Final")

    //RESTful
    compile("javax.ws.rs:javax.ws.rs-api:2.0")
    compile("org.glassfish.jersey.core:jersey-common:2.22.2")
    compile("org.springframework.hateoas:spring-hateoas:0.23.0.RELEASE")

    //Calculations
    compile("com.google.guava:guava:23.5-jre")
    compile("org.apache.commons:commons-lang3:3.7")

    //Documentation
    compile("io.springfox:springfox-swagger2:2.7.0")
    compile("io.springfox:springfox-swagger-ui:2.7.0")

    //Tests
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.springframework.security:spring-security-test")
    testCompile("com.jayway.jsonpath:json-path")
    testCompile("com.h2database:h2:1.4.196")
    testCompile("org.codehaus.groovy:groovy-all:2.4.13")
    testCompile("org.spockframework:spock-core:1.1-groovy-2.4")
}

compileJava {
    options.compilerArgs = [
            '-Amapstruct.suppressGeneratorTimestamp=true',
            '-Amapstruct.suppressGeneratorVersionInfoComment=true',
    ]
}

flyway {
    url = 'jdbc:mysql://localhost:3306/food_finder?autoReconnect=true&useSSL=false&useUnicode=true&useJDBCCompliantTimezoneShift=true&useLegacyDatetimeCode=false&serverTimezone=UTC'
    user = 'root'
    password = 'zoliborz'
    schemas = ['food_finder']
}