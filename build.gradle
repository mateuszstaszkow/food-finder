import io.franzbecker.gradle.lombok.task.DelombokTask

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE")
    }
}

plugins {
    id 'net.ltgt.apt' version '0.8'
    id "org.flywaydb.flyway" version "5.0.3"
    id "com.github.lkishalmi.gatling" version "0.4.1"
    id 'io.franzbecker.gradle-lombok' version '1.11'
    id 'java'
}

apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'war'
apply plugin: 'scala'

war {
    baseName = 'food-finder'
    version = '1.0.0'
}

repositories {
    mavenCentral()
    maven { url "https://repository.jboss.org/nexus/content/repositories/releases" }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    //Spring related
    compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-security:${springBootVersion}")

    //Database configuration
    compile("mysql:mysql-connector-java:${mysqlConnectorVersion}")
    compile("com.zaxxer:HikariCP:${hikariVersion}")
    compile("org.flywaydb:flyway-core:${flywayVersion}")

    //Boilerplate generation
    compile("org.projectlombok:lombok:${lombokVersion}")
    compile("org.mapstruct:mapstruct-jdk8:${mapstructVersion}")
    apt("org.projectlombok:lombok:${lombokVersion}")
    apt("org.mapstruct:mapstruct-processor:${mapstructVersion}")

    //RESTful
    compile("org.glassfish.jersey.core:jersey-common:${jerseyCommonVersion}")
    compile("org.springframework.hateoas:spring-hateoas:${springHateoasVersion}")

    //Calculations
    compile("com.google.guava:guava:${guavaVersion}")
    compile("org.apache.commons:commons-lang3:${apacheCommonsVersion}")

    //Documentation
    compile("io.springfox:springfox-swagger2:${swaggerVersion}")
    compile("io.springfox:springfox-swagger-ui:${swaggerVersion}")
    compile("javax.interceptor:javax.interceptor-api:${javaxInterceptorVersion}")

    //Tests
    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    testCompile("org.springframework.security:spring-security-test:${springVersion}")
    testCompile("com.h2database:h2:${h2Version}")
    testCompile("com.github.tomakehurst:wiremock:${wiremockVersion}")
    compile("org.scala-lang:scala-library:${scalaVersion}")
}

compileJava {
    options.compilerArgs = [
            '-Amapstruct.suppressGeneratorTimestamp=true',
            '-Amapstruct.suppressGeneratorVersionInfoComment=true'
    ]
    options.encoding = "UTF-8"
}

flyway {
    url = 'jdbc:mysql://localhost:3306/food_finder?autoReconnect=true&useSSL=true&useUnicode=true&useJDBCCompliantTimezoneShift=true&useLegacyDatetimeCode=false&serverTimezone=UTC'
    user = 'root'
    password = 'zoliborz'
    schemas = ['food_finder']
}

ScalaCompileOptions.metaClass.daemonServer = true
ScalaCompileOptions.metaClass.fork = true
ScalaCompileOptions.metaClass.useAnt = false
ScalaCompileOptions.metaClass.useCompileDaemon = false

task delombok(type: DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file("$buildDir/delombok")
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
}

task delombokHelp(type: DelombokTask) {
    args "--help"
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
    failOnError = false
}